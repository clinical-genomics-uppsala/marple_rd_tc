__author__ = "Arielle R. Munters"
__copyright__ = "Copyright 2023, Arielle R. Munters"
__email__ = "arielle.munters@scilifelab.uu.se"
__license__ = "GPL-3"


# Include pipeline specific rules
include: "rules/common.smk"
include: "rules/add_ref_to_vcf.smk"
include: "rules/exomedepth_export.smk"
include: "rules/export_qc.smk"
include: "rules/make_output.smk"
include: "rules/sample_order_multiqc.smk"
include: "rules/sample_prep.smk"
include: "rules/tsv2vcf.smk"


report: "report/workflow.rst"


ruleorder: _copy_alignment_crai_file > compression_samtools_index


# 'All' rule, must be specified before any other modules are
# included, since they also contain 'All' rule
rule all:
    input:
        compile_output_file_list,


# Include modules
module alignment:
    snakefile:
        get_module_snakefile(
            config,
            "hydra-genetics/alignment",
            path="workflow/Snakefile",
            tag=config["modules"]["alignment"],
        )
    config:
        config


use rule * from alignment as alignment_*


module annotation:
    snakefile:
        get_module_snakefile(
            config,
            "hydra-genetics/annotation",
            path="workflow/Snakefile",
            tag=config["modules"]["annotation"],
        )
    config:
        config


use rule vep from annotation as annotation_vep


module cnv_sv:
    snakefile:
        get_module_snakefile(
            config,
            "hydra-genetics/cnv_sv",
            path="workflow/Snakefile",
            tag=config["modules"]["cnv_sv"],
        )
    config:
        config


use rule * from cnv_sv as cnv_sv_*


use rule exomedepth_call from cnv_sv as cnv_sv_exomedepth_call with:
    input:
        bam="alignment/samtools_merge_bam/{sample}_{type}.bam",
        bai="alignment/samtools_merge_bam/{sample}_{type}.bam.bai",
        bedfile=config.get("exomedepth_call", {}).get("bedfile", ""),
        ref_count=config["exomedepth_call"]["ref_count"],
        genes=config.get("exomedepth_call", {}).get("genesfile", ""),
        exons=config.get("exomedepth_call", {}).get("exonsfile", ""),


use rule melt from cnv_sv as cnv_sv_melt


module compression:
    snakefile:
        get_module_snakefile(
            config,
            "hydra-genetics/compression",
            path="workflow/Snakefile",
            tag=config["modules"]["compression"],
        )
    config:
        config


use rule * from compression as compression_*


module filtering:
    snakefile:
        get_module_snakefile(config, "hydra-genetics/filtering", path="workflow/Snakefile", tag=config["modules"]["filtering"])
    config:
        config


use rule bcftools_view from filtering as filtering_bcftools_view_region with:
    input:
        vcf="snv_indels/deepsomatic_t_only/{sample}_{type}.vcf.gz",
    output:
        vcf="snv_indels/deepsomatic_t_only/{sample}_{type}_4genes.vcf.gz",
    params:
        extra=config.get("bcftools_view_fourgene", {}).get("extra", ""),
    log:
        "cnv_sv/fourgene_call/{sample}_{type}.view_4gene.vcf.log",
    benchmark:
        repeat(
            "cnv_sv/fourgene_call/{sample}_{type}.view_4gene.vcf.benchmark.tsv",
            config.get("bcftools_view_fourgene", {}).get("benchmark_repeats", 1),
        )


use rule bcftools_view from filtering as filtering_bcftools_view_deepsomatic with:
    input:
        vcf="snv_indels/deepsomatic_t_only/{sample}_{type}_4genes.vcf.gz",
        tbi="snv_indels/deepsomatic_t_only/{sample}_{type}_4genes.vcf.gz.tbi",
    output:
        vcf=temp("snv_indels/bcftools_view/{sample}_{type}.ds.vcf.gz"),
    log:
        "snv_indels/bcftools_view/{sample}_{type}.ds.vcf.log",
    params:
        extra=config.get("bcftools_view_deepsomatic", {}).get("extra", ""),
    benchmark:
        repeat(
            "snv_indels/bcftools_view/{sample}_{type}.ds.benchmark.tsv",
            config.get("bcftools_view", {}).get("benchmark_repeats", 1),
        )


module prealignment:
    snakefile:
        get_module_snakefile(
            config,
            "hydra-genetics/prealignment",
            path="workflow/Snakefile",
            tag=config["modules"]["prealignment"],
        )
    config:
        config


use rule * from prealignment as prealignment_*


module qc:
    snakefile:
        get_module_snakefile(
            config,
            "hydra-genetics/qc",
            path="workflow/Snakefile",
            tag=config["modules"]["qc"],
        )
    config:
        config


use rule * from qc as qc_*


use rule mosdepth_bed from qc as qc_mosdepth_bed with:
    input:
        bam="alignment/samtools_merge_bam/{sample}_{type}.bam",
        bai="alignment/samtools_merge_bam/{sample}_{type}.bam.bai",
        bed=config["reference"]["exon_bed"],
    output:
        bed=temp("qc/mosdepth_bed/{sample}_{type}.regions.bed.gz"),
        bed_csi=temp("qc/mosdepth_bed/{sample}_{type}.regions.bed.gz.csi"),
        coverage=temp("qc/mosdepth_bed/{sample}_{type}.per-base.bed.gz"),
        coverage_csi=temp("qc/mosdepth_bed/{sample}_{type}.per-base.bed.gz.csi"),
        thresholds=temp("qc/mosdepth_bed/{sample}_{type}.thresholds.bed.gz"),
        glob=temp("qc/mosdepth_bed/{sample}_{type}.mosdepth.global.dist.txt"),
        region=temp("qc/mosdepth_bed/{sample}_{type}.mosdepth.region.dist.txt"),
        summary=temp("qc/mosdepth_bed/{sample}_{type}.mosdepth.summary.txt"),
    params:
        thresholds=config["mosdepth_bed"]["thresholds"],


use rule picard_collect_multiple_metrics from qc as qc_picard_collect_multiple_metrics with:
    input:
        bam="alignment/samtools_merge_bam/{sample}_{type}.bam",
        ref=config.get("reference", {}).get("fasta", ""),
        intervals=config["reference"]["design_intervals"],
    params:
        extra=lambda wildcards, input: f" INTERVALS={input.intervals}",


use rule samtools_stats from qc as qc_samtools_stats with:
    params:
        extra="%s " % (config.get("samtools_stats", {}).get("extra", ""),),


use rule multiqc from qc as qc_multiqc with:
    input:
        files=lambda wildcards: set(
            [
                file.format(sample=sample, type=u.type, lane=u.lane, flowcell=u.flowcell, barcode=u.barcode, read=read, ext=ext)
                for file in config["multiqc"]["reports"][wildcards.report]["qc_files"]
                for sample in get_samples(samples)
                for u in units.loc[sample].dropna().itertuples()
                if u.type in config["multiqc"]["reports"][wildcards.report]["included_unit_types"]
                for read in ["fastq1", "fastq2"]
                for ext in config.get("picard_collect_multiple_metrics", {}).get("output_ext", [""])
            ]
        ),
        config=lambda wildcards: config["multiqc"]["reports"][wildcards.report]["config"],
        sample_order="qc/multiqc/sample_order.tsv",
        sample_replacement="qc/multiqc/sample_replacement.tsv",
    params:
        extra=lambda wildcards, input: "--replace-names "
        + input.sample_replacement
        + " --sample-names "
        + input.sample_order
        + " -c "
        + input.config,


module snv_indels:
    snakefile:
        get_module_snakefile(config, "hydra-genetics/snv_indels", path="workflow/Snakefile", tag=config["modules"]["snv_indels"])
    config:
        config


use rule * from snv_indels as snv_indels_*


use rule deepmosaic_draw from snv_indels as snv_indels_deepmosaic_draw with:
    input:
        annovar=config.get("reference", {}).get("annovar", ""),
        bam="alignment/samtools_merge_bam/{sample}_{type}.bam",
        bai="alignment/samtools_merge_bam/{sample}_{type}.bam.bai",
        txt="snv_indels/deepmosaic/{sample}_{type}.input.txt",
        vcf="snv_indels/bcftools_view/{sample}_{type}.ds.vcf.gz",


use rule deepmosaic_input from snv_indels as snv_indels_deepmosaic_input with:
    input:
        bam="alignment/samtools_merge_bam/{sample}_{type}.bam",
        bai="alignment/samtools_merge_bam/{sample}_{type}.bam.bai",
        sex="sample_sex.tsv",
        vcf="snv_indels/bcftools_view/{sample}_{type}.ds.vcf.gz",


use rule deepmosaic_predict from snv_indels as snv_indels_deepmosaic_predict


use rule deepvariant from snv_indels as snv_indels_deepvariant with:
    input:
        bam="alignment/picard_mark_duplicates/{sample}_{type}_{chr}.bam",
        bai="alignment/picard_mark_duplicates/{sample}_{type}_{chr}.bam.bai",
        ref=config.get("reference", {}).get("fasta", ""),
        bed="snv_indels/bed_split/design_bedfile_{chr}.bed",
    params:
        model_type=config.get("deepvariant", {}).get("model_type", ""),
        output_gvcf=lambda wildcards: get_gvcf_output(wildcards, "deepvariant"),
        int_res=lambda wildcards: f"snv_indels/deepvariant/{wildcards.sample}_{wildcards.type}_{wildcards.chr}",
        regions=lambda wildcards, input: f" --regions {input.bed} ",
        extra=config.get("deepvariant", {}).get("extra", ""),


use rule deepsomatic_t_only from snv_indels as snv_indels_deepsomatic_t_only with:
    input:
        bam="alignment/samtools_merge_bam/{sample}_{type}.bam",
        bai="alignment/samtools_merge_bam/{sample}_{type}.bam.bai",
        ref=config.get("reference", {}).get("fasta", ""),
        bed=config.get("reference", {}).get("design_bed", ""),


use rule fix_af from snv_indels as snv_indels_fix_af_g with:
    input:
        vcf="snv_indels/deepvariant/{sample}_{type}.merged.g.vcf.gz",
        tbi="snv_indels/deepvariant/{sample}_{type}.merged.g.vcf.gz.tbi",
    output:
        vcf=temp("snv_indels/deepvariant/{sample}_{type}.g.fix_af.vcf"),
    log:
        "snv_indels/deepvariant/{sample}_{type}.g.fix_af.vcf.log",
    benchmark:
        repeat(
            "snv_indels/deepvariant/{sample}_{type}.fix_af.g.vcf.benchmark.tsv",
            config.get("fix_af", {}).get("benchmark_repeats", 1),
        )
